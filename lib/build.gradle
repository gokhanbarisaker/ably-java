apply plugin: 'java'
apply from: 'maven.gradle'

sourceCompatibility = 1.7
targetCompatibility = 1.7

dependencies {
  compile 'org.msgpack:msgpack-core:0.7.0-p9'
  compile 'org.java-websocket:Java-WebSocket:1.3.1'
  compile 'com.google.code.gson:gson:2.4'
  testCompile 'junit:junit:4.12'
  testCompile 'com.nanohttpd:nanohttpd:2.1.0'
}

task fullJar(type: Jar) {
  baseName = project.name + '-full'
  from {
    configurations.compile.collect {
      it.isDirectory() ? it : zipTree(it)
    }
  }
  with jar
  exclude 'META-INF/**'
}

task testsJar(type: Jar) { 
  baseName = project.name + '-tests'
  from sourceSets.test.output
  classifier = 'test'
  includes = ['**/test/**','**/common/**']
  excludes = ['**/jre/**']
  includeEmptyDirs false
  with jar
}

configurations {
  fullConfiguration
  testsConfiguration
}

artifacts {
  fullConfiguration fullJar
  testsConfiguration testsJar
}

task testRealtimeSuite(type: Test) {
  include '**/RealtimeSuite.class'
  exclude '**/*Test.class'
  beforeTest { descriptor ->
    logger.lifecycle("Running test: " + descriptor)
  }
  outputs.upToDateWhen { false }
}

task testRestSuite(type: Test) {
  include '**/RestSuite.class'
  exclude '**/*Test.class'
  beforeTest { descriptor ->
    logger.lifecycle("Running test: " + descriptor)
  }
  outputs.upToDateWhen { false }
}
